#version 300 es 
precision highp float;
in vec2 fragTexCoord;
in float vNear;
in float vFar;

out vec4 finalColor;

uniform highp sampler2D u_Tex;
uniform vec2 u_dir;

uniform float u_radius;
float sigma_s = 10.0f; // 空间权重的标准差
float sigma_r = 10.5f; // 范围权重的标准差

void main() {
    // vec4 centerColor = texture(rawScreen, fragTexCoord);
    float centerDepth = texture(u_Tex, fragTexCoord).r;
    if(centerDepth <= 0.0f || centerDepth >= 5000.0f) {
        finalColor = vec4(centerDepth, 0.0f, 0.0f, 1.0f);
        return;
    }
    float sum = 0.0f;
    float weightSum = 0.0f;

    float twoSigmaS2 = 2.0f * sigma_s * sigma_s;
    float twoSigmaR2 = 2.0f * sigma_r * sigma_r;

    vec2 texSize = vec2(textureSize(u_Tex, 0));
    for(float i = -u_radius; i <= u_radius; i++) {
        vec2 sampleCoord = fragTexCoord + (u_dir * i) / texSize;
        float sampleDepth = texture(u_Tex, sampleCoord).r;

        //vec4 sampleColor = texture(rawScreen, sampleCoord);
        if(sampleDepth <= 0.0f || sampleDepth >= 10000.0f)
            continue;
        float depthDiff = sampleDepth - centerDepth;
        float rangeWeight = exp(-(depthDiff * depthDiff) / twoSigmaR2);
        float spatialWeight = exp(-(i * i) / twoSigmaS2);
        float weight = spatialWeight * rangeWeight;

        sum += sampleDepth * weight;
        weightSum += weight;
    }
    if(weightSum > 0.0f) {
        finalColor = vec4(sum / weightSum, sum / weightSum, sum / weightSum, 1.0f);
    } else {
        finalColor = vec4(centerDepth, centerDepth, centerDepth, 1.0f);
    }
    //finalColor = vec4(centerDepth, centerDepth, centerDepth, 1);
}